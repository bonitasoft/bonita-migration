import org.bonitasoft.migration.plugin.MigrationConstants

buildscript {
    repositories {
        mavenLocal()
        maven { url = 'http://repositories.rd.lan/archiva/repository/releases/' }
    }
    dependencies {
        classpath 'org.bonitasoft.migration.plugin:bonita-migration-plugins:1.0.4-SNAPSHOT'
    }
}
//define bonita version for all projects
allprojects {
    apply plugin: 'maven'
    repositories {
        mavenLocal()
        maven { url = 'http://repositories.rd.lan/archiva/repository/releases/' }
        mavenCentral()
    }

    version '2.6.0-SNAPSHOT'
    group 'org.bonitasoft.migration'
    ext.isSP = false
    ext.bonitaVersions = [
            '7.0.0',
            '7.0.1',
            '7.0.2',
            '7.0.3',
            '7.1.0',
            '7.1.2',
            '7.1.3',
            '7.2.0'

    ]
    ext.overridedVersions = [
            '7.2.0': '7.2.0-SNAPSHOT'
    ]

    configurations {
        deployerJars
    }

    dependencies {
        deployerJars "org.apache.maven.wagon:wagon-http:2.2"
    }

    uploadArchives {
        repositories.mavenDeployer {
            configuration = configurations.deployerJars

            repository(url: System.getProperty("altDeploymentRepository") != null ? System.getProperty("altDeploymentRepository").split("::").last() : null)
        }
    }
}

apply plugin: 'migration-project'
configure(project.subprojects.findAll {
    it.name.startsWith(MigrationConstants.MIGRATION_PREFIX)
}) {
    apply plugin: 'groovy'
    apply plugin: 'java'
    apply plugin: 'clean-db'
    apply plugin: 'test-migration'

    database {
        dbvendor = System.getProperty("db.vendor", "postgres")
        dburl = System.getProperty("db.url", "jdbc:postgresql://localhost:5432/migration");
        dbuser = System.getProperty("db.user", "bonita")
        dbpassword = System.getProperty("db.password", "bpm")
        dbdriverClass = System.getProperty("db.driverClass", "org.postgresql.Driver")
        dbRootUser = System.getProperty("db.root.user", "postgres")
        dbRootPassword = System.getProperty("db.root.password", "postgres")
        classpath = project.configurations.drivers
    }
    dependencies {
        compile group: 'org.bonitasoft.migration', name: 'bonita-migration-common', version: project.getVersion()
        compile group: 'org.bonitasoft.migration', name: 'bonita-migration-distrib', version: project.getVersion()
        compile group: 'org.postgresql', name: 'postgresql', version: '9.3-1102-jdbc41'
        compile group: 'mysql', name: 'mysql-connector-java', version: '5.1.26'
        compile group: 'com.oracle', name: 'ojdbc', version: '6'
        compile group: 'com.microsoft.jdbc', name: 'sqlserver', version: '4.0.2206.100'
        testCompile group: 'org.assertj', name: 'assertj-core', version: '1.5.0'
    }

    task("testJar", type: Jar) {
        classifier = 'tests'
        from sourceSets.test.output
    }

    artifacts {
        archives tasks.testJar
    }
    tasks.install.dependsOn tasks.testJar
}

migrationConf {
}
