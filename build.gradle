import org.bonitasoft.migration.plugin.MigrationConstants

buildscript {
    repositories {
        mavenLocal()
        maven { url 'http://repositories.rd.lan/maven/releases/' }
    }
    dependencies {
        classpath 'org.bonitasoft.migration.plugin:bonita-migration-plugins:2.22.0-SNAPSHOT'
    }
}

allprojects {
    apply plugin: 'maven'

    repositories {
        mavenLocal()
        maven { url 'http://repositories.rd.lan/maven/releases/' }
        maven { url 'http://repositories.rd.lan/maven/snapshots/' }
        mavenCentral()
    }

    version '2.22.0-SNAPSHOT'
    group 'org.bonitasoft.migration'

    ext.isSP = false
    ext.bonitaVersions = [
            '7.0.0',
            '7.0.1',
            '7.0.2',
            '7.0.3',
            '7.1.0',
            '7.1.2',
            '7.1.3',
            '7.1.4',
            '7.1.5',
            '7.2.0',
            '7.2.1',
            '7.2.2',
            '7.2.3',
            '7.2.4',
            '7.3.0',
            '7.3.1',
            '7.3.2',
            '7.3.3',
            '7.4.0',
            '7.4.1',
            '7.4.2',
            '7.4.3'
    ]
    ext.overridedVersions = [
            :
    ]
    configurations {
        deployerJars
    }

    dependencies {
        deployerJars "org.apache.maven.wagon:wagon-http:2.2"
    }

    uploadArchives {
        repositories.mavenDeployer {
            configuration = configurations.deployerJars

            repository(url: System.getProperty("altDeploymentRepository") != null ? System.getProperty("altDeploymentRepository").split("::").last() : null)
        }
    }

}
subprojects {
    apply plugin: 'groovy'

    targetCompatibility = 1.7

    dependencies {
        compile "org.codehaus.groovy:groovy-all:2.4.4"
        testCompile "junit:junit:4.12"
        testRuntime 'com.oracle:ojdbc:6'
        testRuntime 'com.microsoft.jdbc:sqlserver:4.0.2206.100'
    }
}

configure(project.subprojects.findAll {
    it.name.startsWith(MigrationConstants.MIGRATION_PREFIX)
}) {
    apply plugin: 'groovy'
    apply plugin: 'java'
    apply plugin: 'clean-db'
    apply plugin: 'test-migration'

    database {
        dbvendor = System.getProperty("db.vendor", "postgres")
        dburl = System.getProperty("db.url", "jdbc:postgresql://localhost:5432/migration");
        dbuser = System.getProperty("db.user", "bonita")
        dbpassword = System.getProperty("db.password", "bpm")
        dbdriverClass = System.getProperty("db.driverClass", "org.postgresql.Driver")
        dbRootUser = System.getProperty("db.root.user", "postgres")
        dbRootPassword = System.getProperty("db.root.password", "postgres")
        classpath = project.configurations.drivers
    }
    dependencies {
        compile project(':bonita-migration-common')
        compile project(':bonita-migration-distrib')
        compile group: 'org.postgresql', name: 'postgresql', version: '9.3-1102-jdbc41'
        compile group: 'mysql', name: 'mysql-connector-java', version: '5.1.26'
        compile group: 'com.oracle', name: 'ojdbc', version: '6'
        compile group: 'com.microsoft.jdbc', name: 'sqlserver', version: '4.0.2206.100'
        compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.0.11'
        testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
        testCompile group: 'org.assertj', name: 'assertj-core', version: '1.5.0'
    }

    task("testJar", type: Jar) {
        classifier = 'tests'
        from sourceSets.test.output
    }

    artifacts {
        archives tasks.testJar
    }
    tasks.install.dependsOn tasks.testJar
}
